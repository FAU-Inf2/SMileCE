apply plugin: 'com.android.application'

def appSourcesDir = file('src/main/java')

repositories {
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    def getVersionName = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }

    defaultConfig {
        applicationId "de.fau.cs.mad.smile.android.encryption"
        minSdkVersion 19
        targetSdkVersion 22
        versionCode 2
        versionName "1.0"

        //resources generated during build
        resValue "string", "BuildInfo_Git_CommitHash", getVersionName()
    }

    signingConfigs {
        release {
            storeFile file("unknown")
            storePassword "unknown"
            keyAlias "unknown"
            keyPassword "unknown"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError true
        //lintConfig file("$rootProject.projectDir/config/lint/lint.xml")
    }

    sourceSets {
        main {
            java {
                srcDir appSourcesDir
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

preBuild.dependsOn(":smime-api:build")

def supportVersion = "22.2.1"
def spongyVersion = '1.52.0.0'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':smime-api')
    compile 'com.android.support:support-v4:' + supportVersion
    compile 'com.android.support:appcompat-v7:' + supportVersion
    compile 'com.android.support:cardview-v7:' + supportVersion
    compile 'com.android.support:recyclerview-v7:' + supportVersion

    compile group: 'com.madgag.spongycastle', name: 'core', version: spongyVersion
    compile group: 'com.madgag.spongycastle', name: 'prov', version: spongyVersion
    compile group: 'com.madgag.spongycastle', name: 'pkix', version: spongyVersion
    compile group: 'com.madgag.spongycastle', name: 'pg', version: spongyVersion

    compile 'com.github.konradrenner:kore-javamail:1.0.2'
    compile 'net.danlew:android.joda:2.8.2'
    compile 'commons-io:commons-io:2.4'
    compile "com.daimajia.swipelayout:library:1.2.0@aar"
    testCompile 'junit:junit:4.12'
    testCompile('org.robolectric:robolectric:3.0') {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
}

allprojects {
    afterEvaluate { project ->
        def propsFile = rootProject.file('keystore.properties')
        def configName = 'release'

        if (propsFile.exists() && android.signingConfigs.hasProperty(configName)) {
            def props = new Properties()
            props.load(new FileInputStream(propsFile))
            android.signingConfigs[configName].storeFile = file(props['storeFile'])
            android.signingConfigs[configName].storePassword = props['storePassword']
            android.signingConfigs[configName].keyAlias = props['keyAlias']
            android.signingConfigs[configName].keyPassword = props['keyPassword']
        }
    }
}
